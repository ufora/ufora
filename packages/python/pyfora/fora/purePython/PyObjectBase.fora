/***************************************************************************
   Copyright 2015 Ufora Inc.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
****************************************************************************/
/****************
PyObjectBase

This instance is a mixin that defines the basic properties of all Pyfora python objects.

We use it to provide common hooks into the underlying FORA that all instances will implement.
****************/

object {
    __is_pyfora__: PyBool(true);
    __pyfora_builtins__: PyforaBuiltins;
    __pyfora_concat__: fun(other) {
        let asList = [other];
        self + asList
        };
    __str__: fun() {
        PyString("<object>")
        };

    operator()(*args) {
        let callOp = nothing;
        try {
            callOp = self.__call__
            }
        catch(`Exception.InvalidMember(...) or AttributeError(...))
            {
            throw TypeError(PyString("object is not callable"))
            }
        (`Exception.InvalidCall(...))
            {
            throw TypeError(PyString("cannot call object with arguments as given"))
            }

        callOp(*args)
        };

    convert (Bool _) { 
        let nz = 
            try {
                self.__nonzero__
                }
            catch (...)
                {
                return false
                }
        return nz()`(`Convert, Bool);
        };

    operator iterator() {
        let itFun =
            try { 
                self.__iter__ 
                } 
            catch(AttributeError(...) e) {
                throw TypeError("object is not iterable.")
                }

        return itFun()
        };

    __eq__: fun(other) {
        self is other
        };

    left operator!() {
        self.__not__()
        };

    left operator-() {
        self.__neg__()
        };

    operator==(other) {
        self.__eq__(other)
        };

    operator!=(other) {
        return self.__ne__(other)
        };

    operator+(other) {
        self.__add__(other)
        };

    operator-(other) {
        self.__sub__(other)
        };

    operator*(other) {
        self.__mul__(other)
        };

    operator/(other) {
        self.__div__(other)
        };

    operator<(other) {
        self.__lt__(other)
        };

    operator>(other) {
        self.__gt__(other)
        };

    operator**(other) {
        self.__pow__(other)
        };

    operator^(other) {
        self.__xor__(other)
        };

    __ge__: fun(_ other) {
        if(self.__eq__(other)) {
            return PyBool(true)
            }
        return self.__gt__(other)
        };

    operator>=(other) {
        self.__ge__(other)
        };

    operator<=(other) {
        self.__le__(other)
        };

    __le__: fun(_ other) {
        if(self.__eq__(other)) {
            return PyBool(true)
            }
        return self.__lt__(other)
        };

    ...(`Member, x) { 
        throw AttributeError(PyString("object has no attribute " + String(x)))
        };
    };

