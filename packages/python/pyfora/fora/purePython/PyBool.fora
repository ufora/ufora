/***************************************************************************
   Copyright 2015 Ufora Inc.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
****************************************************************************/
class {
    member @m;

    ...(`Member, `@m) { @m };

    __str__: PyBoundMethod(self, fun(self) {
        let tr = match (self.@m) with 
            (true) { "True" }
            (false) { "False" }
            ;
        PyString(tr)
        });

    __class__: BoolType;

    __gt__: PyBoundMethod(self, fun(self, PyBool(other)) {
        if(self.@m == true and other.@m == false)
            return PyBool(true)
        return PyBool(false)
        }
        (self, PyInt(other) or PyFloat(other)) {
            if(other.@m >= 1)
                return PyBool(false)
            if(self.@m == true)
                return PyBool(other.@m < 1)
            else 
                return PyBool(other.@m < 0)
        }
        (self, _) {
        PyBool(false)
        });

    __eq__: PyBoundMethod(self, fun(self, PyInt(other) or PyFloat(other)) {
        if(self.@m) 
            return PyBool(other.@m == 1)
        else
            return PyBool(other.@m == 0)
        }
        (self, PyBool(other)) {
            PyBool(other.@m == self.@m)
        }
        (self, _) {
        PyBool(false)
        });

    __abs__: PyBoundMethod(self, fun(self) {
        if(self.@m)
            return PyInt(1)
        else
            return PyInt(0)
        });

    __neg__: PyBoundMethod(self, fun(self) { if (self.@m) return PyInt(-1) else return PyInt(0) });

    __pos__: PyBoundMethod(self, fun(self) { return PyInt(Int64(self.@m)) });

    __invert__: PyBoundMethod(self, fun(self) { return PyInt(-1 - Int64(self.@m)) });

    __lt__: PyBoundMethod(self, fun(self, PyBool(other)) {
            if(other.@m == true and self.@m == false)
                return PyBool(true)
            return PyBool(false)
            }
            (self, PyInt(other) or PyFloat(other)) {
                if(other.@m > 1)
                    return PyBool(true)
                if(self.@m == true)
                    return PyBool(other.@m > 1)
                else 
                    return PyBool(other.@m > 0)
            }
            (self, _) {
                PyBool(true)
            });

    __mul__: PyBoundMethod(self, fun(self, other) {
        if(self.@m == true)
            other
        else
            PyInt(0)
        });

    __nonzero__: PyBoundMethod(self, fun(self) { self });

    convert (Bool _) { return @m; };

    operator match(PyBool _) { (self,) };

    mixin PyObjectBase;
    };

